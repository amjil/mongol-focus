(ns minii-focus.screens.perspective
  "Perspective page"
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["dart:convert" :as json]
   [cljd.flutter :as f]
   [minii-focus.core.components.core :as comp]
   [minii-focus.core.components.tokens :as tokens]
   [minii-focus.core.state.app-state :as app-state]
   [minii-focus.core.db.perspective :as perspective-db]
   [minii-focus.core.db.task :as task-db]
   [minii-focus.core.event.dispatch :as dispatch]
   [minii-focus.core.utils.toast :as toast]
   [minii-focus.widgets.quick_entry_dialog :as qe-dialog]
   [minii-focus.widgets.common :as widgets]))

(defn format-weight [weight]
  (str (int (* weight 100)) "%"))

(defn perspective-list-item [{:keys [perspective on-tap on-long-press task-stats]}]
  (let [weight-history (widgets/parse-json (.-weightHistory perspective))
        current-weight (.-weight perspective)
        stats (or task-stats {:total 0 :completed 0 :active 0 :waiting 0})]
    (m/Card
     .margin (m/EdgeInsets.symmetric :vertical (tokens/space :s) :horizontal (tokens/space :m))
     .child (m/InkWell
            .onTap (when on-tap (fn [] (on-tap perspective)))
            .onLongPress (when on-long-press (fn [] (on-long-press perspective)))
            .child (m/Padding
                   .padding (m/EdgeInsets.all (tokens/space :m))
                   .child (m/Row
                          .children
                          [(m/Column
                            .mainAxisSize m/MainAxisSize.min
                            .children
                            [(m/Expanded
                              .child (comp/mongol-text-block
                                     {:text (.-title perspective)
                                      :style (m/TextStyle .fontSize (tokens/font-size :m) .fontWeight m/FontWeight.bold)}))
                             (m/Container
                              .padding (m/EdgeInsets.symmetric :horizontal (tokens/space :s) :vertical (tokens/space :xs))
                              .decoration (m/BoxDecoration .color (widgets/get-weight-color current-weight) .borderRadius (m/BorderRadius.circular 12))
                              .child (comp/mongol-text-block
                                     {:text (format-weight current-weight)
                                      :style (m/TextStyle .fontSize (tokens/font-size :s) .color m/Colors.white .fontWeight m/FontWeight.bold)}))])
                            (m/SizedBox .width (tokens/space :s))
                            (m/Column
                             .mainAxisAlignment m/MainAxisAlignment.spaceBetween
                             .children
                             [(m/Row
                               .mainAxisSize m/MainAxisSize.min
                               .children
                               [(m/Icon m/Icons.task .size 16 .color (tokens/color :text-weak))
                                (m/SizedBox .height (tokens/space :xs))
                                (comp/mongol-text-block {:text (str "Tasks: " (:total stats)) :style (m/TextStyle .fontSize (tokens/font-size :s) .color (tokens/color :text-weak))})])
                              (m/Row
                               .mainAxisSize m/MainAxisSize.min
                               .children
                               [(m/Icon m/Icons.check_circle .size 14 .color m/Colors.green)
                                (m/SizedBox .height (tokens/space :xs))
                                (comp/mongol-text-block {:text (str (:completed stats)) :style (m/TextStyle .fontSize (tokens/font-size :s) .color m/Colors.green)})])])
                            (m/SizedBox .width (tokens/space :s))
                            (if (empty? weight-history)
                              (comp/mongol-text-block {:text "No history data" :style (m/TextStyle .fontSize (tokens/font-size :s) .color (tokens/color :text-weak))})
                              (widgets/weight-history-chart {:weight-history weight-history}))]))))))

(defn show-rules-editor [ctx perspective]
  (let [rules (widgets/parse-json (.-rules perspective))
        conditions-ctrl (m/TextEditingController .text (json/jsonEncode (or (:conditions rules) [])))
        actions-ctrl (m/TextEditingController .text (json/jsonEncode (or (:actions rules) [])))]
    (m/showDialog
     .context ctx
     .builder (fn [d-ctx]
                (m/AlertDialog
                 .title (comp/mongol-text-block {:text (str "Edit Rules: " (.-title perspective)) :style (m/TextStyle .fontSize (tokens/font-size :l) .fontWeight m/FontWeight.bold)})
                 .content (m/Row
                           .mainAxisSize m/MainAxisSize.min
                           .children
                           [(comp/mongol-text-block {:text "Conditions (JSON)"})
                            (mgl/MongolTextField .controller conditions-ctrl .maxLines 3)
                            (m/SizedBox .width (tokens/space :m))
                            (comp/mongol-text-block {:text "Actions (JSON)"})
                            (mgl/MongolTextField .controller actions-ctrl .maxLines 3)])
                 .actions
                 [(mgl/MongolTextButton .onPressed (fn [] (m/Navigator.pop d-ctx)) .child (comp/mongol-text-block {:text "Close"}))
                  (mgl/MongolTextButton .onPressed (fn []
                                             (try
                                               (let [new-rules {:conditions (json/jsonDecode (.text conditions-ctrl))
                                                                :actions (json/jsonDecode (.text actions-ctrl))}]
                                                 (m/Navigator.pop d-ctx)
                                                 (-> (dispatch/dispatch-event! ctx {:type :perspective/update-rules :payload {:perspective-id (.-id perspective) :rules new-rules} :source :ui})
                                                     (.then (fn [_] (toast/show-success-toast ctx "Updated")))))
                                               (catch Exception _ (toast/show-error-toast ctx "JSON Error"))))
                                .child (comp/mongol-text-block {:text "Save"}))])))))

(defn perspective-screen [_params]
  (f/widget
   :context ctx
   :watch [dao app-state/dao
           perspectives (when dao (perspective-db/watch-all dao))
           all-tasks (when dao (task-db/watch-all-tasks dao))]
   :let [loading (or (nil? dao) (nil? perspectives) (nil? all-tasks))
         perspectives (or perspectives [])
         all-tasks (or all-tasks [])]
   (m/Scaffold
    .appBar (m/AppBar 
             .actions [(m/IconButton .icon (m/Icon m/Icons.add) .onPressed (fn [] (qe-dialog/show-add-perspective-dialog ctx)))])
    .body (if loading
            (widgets/loading-indicator)
            (if (empty? perspectives)
              (widgets/empty-state "No Perspective")
              (m/ListView
               .scrollDirection m/Axis.horizontal
               .children
               (mapv (fn [p]
                       (let [pts (filter (fn [t] (some #(= % (.-id p)) (widgets/parse-json (.-perspectives t)))) all-tasks)
                             stats {:total (count pts) :completed (count (filter #(.-completed %) pts)) :active (count (filter #(and (not (.-completed %)) (nil? (.-deferAt %))) pts))}]
                         (perspective-list-item {:perspective p :task-stats stats :on-tap (fn [sp] (show-rules-editor ctx sp))})))
                     perspectives)))))))
