(ns minii-focus.screens.inbox
  "Inbox / Quick Entry"
  (:require
   ["package:flutter/material.dart" :as m]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.components.core :as comp]
   [minii-focus.components.dialog :as dialog]
   [minii-focus.screens.task-detail :as task-detail]
   [minii-focus.services.task :as task]
   [minii-focus.states.app-state :as state]))

(defn- side-panel [tasks defer-date due-date]
  (m/Container
   .padding (m/EdgeInsets.all (comp/space :m))
   .child (m/Column
           .crossAxisAlignment m/CrossAxisAlignment.start
           .children
           [(comp/mongol-text-block
             {:text "Inbox overview"
              :style (m/TextStyle .fontSize (comp/font-size :m))})
            (m/SizedBox .height (comp/space :s))
            (comp/mongol-text-block
             {:text (str "Pending " (count tasks))
              :style (m/TextStyle .color (comp/color :text-weak))})
            (m/SizedBox .height (comp/space :s))
            (comp/mongol-text-block
             {:text (if @defer-date
                      (str "Start " (.toString @defer-date))
                      "Start Not set")
              :style (m/TextStyle .color (comp/color :text-weak))})
            (m/SizedBox .height (comp/space :s))
            (comp/mongol-text-block
             {:text (if @due-date
                      (str "Due " (.toString @due-date))
                      "Due Not set")
              :style (m/TextStyle .color (comp/color :text-weak))})])))

(defn inbox-screen []
  (let [content (atom "")
        defer-date (atom nil)
        due-date (atom nil)
        controller (m/TextEditingController.)]
    (f/widget
     :context ctx
     :watch [_ct content
             _de defer-date
             _du due-date
             {tasks :inbox-tasks loading :inbox-loading} state/app-state]
     (m/Scaffold)
     .body
     (m/SafeArea)
     (comp/mongol-row-layout
      {:main-row
       (m/Padding
        .padding (m/EdgeInsets.all (comp/space :m))
        .child
        (m/Row
         .mainAxisSize m/MainAxisSize.max
         .mainAxisAlignment m/MainAxisAlignment.spaceAround
         .children
         [(m/InkWell
           .onTap (fn []
                    (dialog/show-mongol-input-dialog
                     ctx
                     {:controller controller
                      :defer-date defer-date
                      :due-date due-date
                      :content content}))
           .child (m/Container
                   .padding (m/EdgeInsets.all (comp/space :s))
                   .decoration (m/BoxDecoration
                                .border (m/Border.all
                                         .color m/Colors.grey.shade300
                                         .width 1)
                                .borderRadius (m/BorderRadius.circular 4))
                   .child (m/Column
                           .mainAxisSize m/MainAxisSize.max
                           .mainAxisAlignment m/MainAxisAlignment.center
                           .children
                           [(comp/mongol-text-block
                             {:text (if (empty? @content)
                                      "Quick entry"
                                      @content)
                              :style (m/TextStyle
                                      .fontSize (comp/font-size :m)
                                      .color (if (empty? @content)
                                               m/Colors.grey.shade600
                                               m/Colors.black))})])))
          (m/SizedBox .width (comp/space :s))
          (m/Divider)
          (cond
            loading (m/Center .child (m/CircularProgressIndicator))
            (empty? tasks) (comp/mongol-text-block
                            {:text "Inbox is empty"
                             :style (m/TextStyle .color (comp/color :text-weak))})
            :else (comp/mongol-task-list
                   tasks
                   (fn [task]
                     (-> (task/mark-task-completed (.-id task))
                         (.then (fn []
                                  (task/reload-inbox! ctx)
                                  (task/show-toast ctx "Completed" :success)))
                         (.catchError (fn [e]
                                        (dart-core/print e)
                                        (task/show-toast ctx "Update failed" :error)))))
                   (fn [task]
                     ;; Navigate to task detail page
                     (task/refresh-task-detail! (.-id task))
                     (m/Navigator.push
                      ctx
                      (m/MaterialPageRoute
                       .builder (fn [_]
                                  (task-detail/task-detail-screen (.-id task))))))))]))
       :side-row (side-panel tasks defer-date due-date)}))))

;; NOTE: Loading/refresh is handled by services; pages only subscribe to state and trigger refresh via buttons.

