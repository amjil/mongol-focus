(ns minii-focus.screens.project
  "Project page"
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.core.components.core :as comp]
   [minii-focus.core.components.tokens :as tokens]
   [minii-focus.core.state.app-state :as app-state]
   [minii-focus.core.state.ui-state :as ui-state]
   [minii-focus.core.db.project :as project-db]
   [minii-focus.core.event.dispatch :as dispatch]
   [minii-focus.core.utils.toast :as toast]
   [minii-focus.widgets.quick_entry_dialog :as qe-dialog]
   [minii-focus.widgets.common :as widgets]))

(defn is-review-overdue? [review-at]
  (and review-at (> (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch)) review-at)))

(defn get-health-color [health]
  (case health :ok (tokens/color :text-main) :no-next-action m/Colors.red :review-overdue m/Colors.orange (tokens/color :text-weak)))

(defn project-list-item [{:keys [project next-action health on-selected]}]
  (let [is-overdue (is-review-overdue? (.-reviewAt project))
        is-on-hold (= (.-status project) "on-hold")]
    (m/InkWell
     .onTap (fn [] (when on-selected (on-selected project)))
     .child (m/Padding
             .padding (m/EdgeInsets.all (tokens/space :m))
             .child (m/Column
                     .children
                     [(m/Expanded .child (comp/mongol-text-block {:text (.-title project) :style (m/TextStyle .color (get-health-color health))}))
                      (m/Column .children (vec (filter some? [(when (and (not is-on-hold) (nil? next-action)) (m/Icon m/Icons.warning .size 16 .color m/Colors.red))
                                                              (when is-overdue (m/Icon m/Icons.schedule .size 16 .color m/Colors.orange))])))])))))

(defn project-status-card [{:keys [project health on-archive on-restore on-save-template]}]
  (let [status (.-status project)]
    (m/Card
     .margin (m/EdgeInsets.all (tokens/space :m))
     .child (m/Padding
             .padding (m/EdgeInsets.all (tokens/space :l))
             .child (m/Row
                     .crossAxisAlignment m/CrossAxisAlignment.start
                     .children
                     [(comp/mongol-text-block {:text (.-title project) :style (m/TextStyle .fontSize (tokens/font-size :l) .fontWeight m/FontWeight.bold)})
                      (m/SizedBox .width (tokens/space :m))
                      (m/Column 
                       .children [(m/Icon m/Icons.info .size 20 .color (get-health-color health))
                                  (m/SizedBox .height (tokens/space :s))
                                  (comp/mongol-text-block {:text (str "Status: " status) :style (m/TextStyle .color (get-health-color health))})])
                      (m/SizedBox .width (tokens/space :m))
                      (m/Column 
                       .mainAxisAlignment m/MainAxisAlignment.spaceEvenly
                       .children [(mgl/MongolTextButton .onPressed (fn [] (if (= status "archived") (on-restore project) (on-archive project))) .child (comp/mongol-text-block {:text (if (= status "archived") "Restore" "Archive")}))
                                  (m/SizedBox .height (tokens/space :m))
                                  (mgl/MongolTextButton .onPressed (fn [] (on-save-template project)) .child (comp/mongol-text-block {:text "Save Template"}))])])))))

(defn project-progress-card [{:keys [tasks]}]
  (let [total (count tasks)
        done (count (filter #(.-completed %) tasks))
        progress (if (zero? total) 0.0 (/ done total))]
    (m/Card
     .margin (m/EdgeInsets.all (tokens/space :m))
     .child (m/Padding
             .padding (m/EdgeInsets.all (tokens/space :m))
             .child (m/Column
                    .crossAxisAlignment m/CrossAxisAlignment.start
                    .children
                    [(comp/mongol-text-block {:text "Progress" :style (m/TextStyle .fontWeight m/FontWeight.bold)})
                     (m/SizedBox .width (tokens/space :s))
                     (m/SizedBox .width 4.0 .height 100.0 .child (m/LinearProgressIndicator .value progress .backgroundColor m/Colors.grey.shade200))
                     (m/SizedBox .width (tokens/space :s))
                     (comp/mongol-text-block {:text (str done "/" total " tasks")})])))))

(defn project-screen [_params]
  (f/widget
   :context ctx
   :watch [dao app-state/dao
           projects (when dao (project-db/watch-all-projects dao))
           {:keys [selected-project next-actions project-tasks]} ui-state/project-state]
   :let [loading (or (nil? dao) (nil? projects))
         projects (or projects [])
         _ (when (and (not loading) (coll? projects))
             (doseq [p projects]
               (when-not (contains? next-actions (.-id p))
                 (-> (project-db/get-next-action dao (.-id p))
                     (.then (fn [na] (ui-state/update-project-state! update :next-actions assoc (.-id p) na)))))))]
   (m/Scaffold
    .appBar (m/AppBar 
             .actions [(m/IconButton .icon (m/Icon m/Icons.add) .onPressed (fn [] (qe-dialog/show-add-project-dialog ctx)))])
    .body (comp/mongol-column-layout
           {:main-width 0.4 :side-width 0.6
            :main-column (if loading (widgets/loading-indicator)
                             (if (empty? projects)
                               (widgets/empty-state "No projects")
                               (m/ListView
                                .scrollDirection m/Axis.horizontal
                                .children (mapv (fn [p] (project-list-item
                                                         {:project p
                                                          :next-action (get next-actions (.-id p))
                                                          :health (project-db/project-health p (get next-actions (.-id p)))
                                                          :on-selected (fn [sp] (ui-state/update-project-state! assoc :selected-project sp) (-> (project-db/get-project-tasks dao (.-id sp)) (.then (fn [ts] (ui-state/update-project-state! update :project-tasks assoc (.-id sp) ts)))))}))
                                                projects))))
            :side-column (if-let [p selected-project]
                           (m/SingleChildScrollView
                            .scrollDirection m/Axis.horizontal
                            .child
                            (m/Row
                             .children [(project-status-card
                                         {:project p
                                          :health (project-db/project-health p (get next-actions (.-id p)))
                                          :on-archive (fn [ap] (-> (dispatch/dispatch-event! ctx {:type :project/archive :payload {:project-id (.-id ap)} :source :ui})))
                                          :on-restore (fn [rp] (-> (dispatch/dispatch-event! ctx {:type :project/restore :payload {:project-id (.-id rp)} :source :ui})))
                                          :on-save-template (fn [_] (toast/show-info-toast ctx "Template feature in progress"))})
                                        (project-progress-card {:tasks (get project-tasks (.-id p) [])})]))
                           (widgets/empty-state "Select a project"))}))))
