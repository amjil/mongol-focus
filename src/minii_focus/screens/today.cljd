(ns minii-focus.screens.today
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.core.components.core :as comp]
   [minii-focus.core.components.tokens :as tokens]
   [minii-focus.core.state.app-state :as app-state]
   [minii-focus.core.state.ui-state :as ui-state]
   [minii-focus.core.db.task :as task-db]
   [minii-focus.core.db.forecast :as forecast-db]
   [minii-focus.core.db.timeline :as timeline-db]
   [minii-focus.core.utils.time :as time]
   [minii-focus.core.router.nav :as nav]
   [minii-focus.widgets.common :as widgets]))

(defn forecast-item [{:keys [forecast-item task-title on-action]}]
  (let [is-done (.-completed forecast-item)]
    (m/Card
     .margin (m/EdgeInsets.symmetric :vertical (tokens/space :s) :horizontal (tokens/space :m))
     .child (mgl/MongolListTile
             .title (comp/mongol-text-block
                     {:text task-title
                      :style (m/TextStyle
                              .decoration (if is-done m/TextDecoration.lineThrough nil))})
             .trailing (if is-done
                         (m/Icon m/Icons.check_circle .color m/Colors.green)
                         (mgl/MongolIconButton
                          .icon (m/Icon m/Icons.play_arrow)
                          .onPressed (fn [] (on-action forecast-item :start))))))))

(defn today-screen [_params]
  (let [dao (app-state/get-dao)
        now-ts (int (/ (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch)) 1000))
        today-yyyymmdd (time/ts->yyyymmdd now-ts)
        today-start-ts (time/yyyymmdd->ts today-yyyymmdd)
        today-end-ts (+ today-start-ts (* 24 60 60))]

    (f/widget
     :watch [forecasts (forecast-db/watch-by-date dao today-yyyymmdd)
             events (timeline-db/watch-range dao today-start-ts today-end-ts)
             {:keys [task-titles-map]} ui-state/today-state]
     :let [loading (or (nil? forecasts) (nil? events))
           forecasts (or forecasts [])
           stats (ui-state/calculate-today-stats events)
           ;; Update focus-state for other screens
           _ (when (and (not loading) (not= forecasts (:today-forecasts @ui-state/focus-state)))
               (ui-state/update-focus-state! assoc :today-forecasts forecasts))
           _ (when (and (not loading) (not= stats (:today-stats @ui-state/focus-state)))
               (ui-state/update-focus-state! assoc :today-stats stats))
           _ (when (and (not loading) (coll? forecasts))
               (let [tids (distinct (map #(.-taskId %) forecasts))]
                 (doseq [tid tids]
                   (when-not (get task-titles-map tid)
                     (-> (task-db/get-by-id dao tid)
                         (.then (fn [t] 
                                  (when t 
                                    (ui-state/update-today-state! update :task-titles-map assoc tid (.-title t))
                                    (ui-state/update-focus-state! update :task-titles-map assoc tid (.-title t))))))))))]
     (m/Scaffold
      .appBar (m/AppBar)
      .body (m/Column
             .children
             [(m/Padding
               .padding (m/EdgeInsets.all (tokens/space :m))
               .child (m/Row
                       .mainAxisAlignment m/MainAxisAlignment.spaceBetween
                       .children
                       [(comp/mongol-text-block
                         {:text (str "Today's Forecast: " (count forecasts))
                          :style (m/TextStyle .fontSize (tokens/font-size :m) .fontWeight m/FontWeight.bold)})
                        (m/ElevatedButton
                         .onPressed (fn [] 
                                      (ui-state/init-focus-screen!)
                                      (nav/navigate! :focus {}))
                         .child (m/Text "Start Focus"))]))
              (m/Expanded
               .child (if loading
                        (widgets/loading-indicator)
                        (if (empty? forecasts)
                          (widgets/empty-state "No tasks for today")
                          (m/ListView
                           .children
                           (mapv (fn [f]
                                   (forecast-item
                                    {:forecast-item f
                                    :task-title (get task-titles-map (.-taskId f) "Loading...")
                                    :on-action (fn [_ _] 
                                                 (ui-state/init-focus-screen!)
                                                 (nav/navigate! :focus {}))}))
                                 forecasts)))))])))))
