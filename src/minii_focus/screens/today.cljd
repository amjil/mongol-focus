(ns minii-focus.screens.today
  "Today page - display today's tasks"
  (:require
   ["package:flutter/material.dart" :as m]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.components.core :as comp]
   [minii-focus.screens.task-detail :as task-detail]
   [minii-focus.services.task :as task]
   [minii-focus.states.app-state :as state]
   [minii-focus.utils.toast :as toast]))

    
(defn main-view [ctx tasks-with-info show-blocked selection-mode selected-task-ids refresh-today-with-blocked!]
  (f/widget
   :watch [_twi tasks-with-info
           _sb show-blocked
           _selection-mode selection-mode
           _selected-ids selected-task-ids
           {loading :today-loading} state/app-state]
   (cond
     loading
     (m/Center
      .child (m/CircularProgressIndicator))

     (empty? _twi)
     (m/Padding
      .padding (m/EdgeInsets.all (comp/space :xl))
      .child (comp/mongol-text-block
              {:text "No tasks today"
               :style (m/TextStyle
                       .color (comp/color :text-weak)
                       .fontSize (comp/font-size :m))}))


     :else
     (m/ListView
      .scrollDirection m/Axis.horizontal
      .padding (m/EdgeInsets.all (comp/space :m))
      .children
      (let [visible-tasks (->> _twi
                               (filter (fn [info]
                                         (or _sb
                                             (not (:blocked info)))))
                               (vec))]
        (map-indexed
         (fn [index info]
           (let [task-row (:task info)
                 task-id (.-id task-row)
                 task-status (keyword (.-status task-row))
                 blocked (:blocked info)
                 ;; Use previous visible task as default parent for "Set as child"
                 parent-task-id (when (pos? index)
                                  (.-id task-row))]
             (let [is-selected (contains? @selected-task-ids task-id)
                   handlers (comp/make-task-handlers
                            ctx task-id task-row task-status
                            selected-task-ids selection-mode
                            {:on-navigate-to-task (fn [tid]
                                                   (task/refresh-task-detail! tid)
                                                   (m/Navigator.push
                                                    ctx
                                                    (m/MaterialPageRoute
                                                     .builder (fn [_]
                                                                (task-detail/task-detail-screen tid)))))
                             :on-refresh refresh-today-with-blocked!
                             :on-complete-success (fn []
                                                   (task/refresh-today-tasks!)
                                                   (task/refresh-focus-tasks!))
                             :on-enter-focus-success (fn []
                                                      (task/refresh-today-tasks!)
                                                      (task/refresh-focus-tasks!))
                             :on-long-press-menu (fn [ctx tid blocked? parent-id on-refresh]
                                                  (comp/show-structure-menu
                                                   ctx tid blocked? parent-id on-refresh
                                                   :on-navigate-to-task (fn [parent-id]
                                                                          (task/refresh-task-detail! parent-id)
                                                                          (m/Navigator.push
                                                                           ctx
                                                                           (m/MaterialPageRoute
                                                                            .builder (fn [_]
                                                                                       (task-detail/task-detail-screen parent-id)))))))
                             :blocked? blocked
                             :parent-task-id parent-task-id})]
               (comp/mongol-task-item
                (merge
                 {:content (.-content task-row)
                  :status task-status
                  :blocked? blocked
                  :selection-mode _selection-mode
                  :selected is-selected}
                 handlers)))))
         visible-tasks))))))

(defn today-screen []
  (let [tasks-with-info (atom [])
        show-blocked (atom true)
        selection-mode (atom false)
        selected-task-ids (atom #{})]
    (f/widget
     :context ctx
     ;; Pages only subscribe to state, don't do loading side effects in render
     :watch [_app state/app-state
             {loading :today-loading tasks :today-tasks} state/app-state
             _twi tasks-with-info
             _sb show-blocked
             _selection-mode selection-mode
             _selected-ids selected-task-ids]
     :let [;; Helper: refresh Today tasks and blocked info
           refresh-today-with-blocked!
           (fn []
             (reset! tasks-with-info [])
             (task/refresh-today-tasks!)
             (-> (task/get-today-tasks)
                 (.then (fn [refreshed-tasks]
                          (dart-core/print (str "Refreshed tasks: " refreshed-tasks))
                          (when (seq refreshed-tasks)
                            (-> (task/get-tasks-with-blocked-info refreshed-tasks)
                                (.then (fn [info-list]
                                         (dart-core/print (str "Info list: " info-list))
                                         (reset! tasks-with-info info-list)))
                                (.catchError (fn [error]
                                               (toast/handle-error-with-toast ctx error "Хориглосон мэдээллийг ачаалахад алдаа гарлаа")))))))
                 (.catchError (fn [error]
                                (toast/handle-error-with-toast ctx error "Өнөөдрийн ажлыг шинэчлэхэд алдаа гарлаа")))))
           ]
      ;; (when (and (not loading) (seq tasks) (empty? _twi))
      ;;   (-> (task/get-tasks-with-blocked-info tasks)
      ;;       (.then (fn [info-list]
      ;;                (reset! tasks-with-info info-list)))
      ;;       (.catchError (fn [error]
      ;;                      (toast/handle-error-with-toast ctx error "Хориглосон мэдээллийг ачаалахад алдаа гарлаа")))))
     (m/Scaffold
      .appBar (m/AppBar
               .actions [(if _selection-mode
                           (m/IconButton
                            .tooltip "Cancel selection"
                            .icon (m/Icon m/Icons.close)
                            .onPressed (fn []
                                         (reset! selected-task-ids #{})
                                         (reset! selection-mode false)))
                           (m/IconButton
                            .tooltip "Select tasks"
                            .icon (m/Icon m/Icons.check_box)
                            .onPressed (fn []
                                         (reset! selection-mode true))))
                         (if (not _selection-mode)
                           (m/IconButton
                            .tooltip "Toggle blocked tasks"
                            .icon (m/Icon (if _sb m/Icons.visibility_off m/Icons.visibility))
                            .onPressed (fn []
                                         (swap! show-blocked not)))
                           (m/SizedBox))
                         (if (not _selection-mode)
                           (m/IconButton
                            .tooltip "Refresh"
                            .icon (m/Icon m/Icons.refresh)
                            .onPressed (fn []
                                         (refresh-today-with-blocked!)))
                           (m/SizedBox))])
      .body (m/Column
             .children
             [(comp/batch-operations-toolbar ctx selected-task-ids selection-mode refresh-today-with-blocked!)
              (m/Expanded
               .child (comp/mongol-row-layout
                       {:main-row (comp/vertical-reveal-motion
                                   {:child (main-view ctx tasks-with-info show-blocked selection-mode selected-task-ids refresh-today-with-blocked!)})
                        :side-row (comp/today-overview-panel tasks @tasks-with-info)}))])))))


