(ns minii-focus.screens.focus
  "Focus page"
  (:require
   ["package:flutter/material.dart" :as m]
   ["dart:core" :as dart-core]
   ["dart:async" :as async]
   [cljd.flutter :as f]
   [minii-focus.core.components.core :as comp]
   [minii-focus.core.components.tokens :as tokens]
   [minii-focus.core.components.focus-depth :as focus-depth]
   [minii-focus.core.state.app-state :as app-state]
   [minii-focus.core.state.ui-state :as ui-state]
   [minii-focus.core.db.task :as task-db]
   [minii-focus.core.db.forecast :as forecast-db]
   [minii-focus.core.db.timeline :as timeline-db]
   [minii-focus.core.event.dispatch :as dispatch]
   [minii-focus.core.utils.time :as time]
   [minii-focus.core.utils.toast :as toast]
   [minii-focus.widgets.common :as widgets]))

(def pomodoro-duration-seconds (* 25 60))

(defn stats-item [{:keys [label value color]}]
  (m/Column
   .crossAxisAlignment m/CrossAxisAlignment.center
   .children
   [(comp/mongol-text-block
     {:text (str value)
      :style (m/TextStyle
              .fontSize (tokens/font-size :xl)
              .fontWeight m/FontWeight.bold
              .color (or color (tokens/color :accent)))})
    (comp/mongol-text-block
     {:text label
      :style (m/TextStyle
              .fontSize (tokens/font-size :s)
              .color (tokens/color :text-weak))})]))

(defn today-stats-card [{:keys [stats depth-history]}]
  (m/Card
   .margin (m/EdgeInsets.all (tokens/space :m))
   .child (m/Padding
           .padding (m/EdgeInsets.all (tokens/space :m))
           .child (m/Column
                   .crossAxisAlignment m/CrossAxisAlignment.start
                   .children
                   [(comp/mongol-text-block
                     {:text "Today's Statistics"
                      :style (m/TextStyle
                              .fontSize (tokens/font-size :m)
                              .fontWeight m/FontWeight.bold)})
                    (m/SizedBox .height (tokens/space :s))
                    (m/Row
                     .mainAxisAlignment m/MainAxisAlignment.spaceAround
                     .children
                     [(stats-item {:label "minutes" :value (:total-minutes stats)})
                      (m/SizedBox .height (tokens/space :m))
                      (when (seq depth-history)
                        (focus-depth/focus-depth-chart
                         {:depths depth-history :max-items 7}))
                      (stats-item {:label "sessions" :value (:session-count stats)})
                      (stats-item {:label "Completed" :value (:completed-count stats) :color m/Colors.green})
                      (stats-item {:label "Interruptions" :value (:pause-count stats) :color m/Colors.orange})])]))))

(defn focus-active-session [{:keys [task-title remaining-seconds current-focus-depth is-paused on-resume on-pause on-complete on-abort]}]
  (m/Center
   .child (m/Column
           .mainAxisAlignment m/MainAxisAlignment.center
           .crossAxisAlignment m/CrossAxisAlignment.center
           .children
           [(comp/mongol-text-block
             {:text task-title
              :style (m/TextStyle
                      .fontSize (tokens/font-size :xl)
                      .fontWeight m/FontWeight.bold)})
            (m/SizedBox .height (tokens/space :l))
            (comp/mongol-text-block
             {:text (widgets/format-timer remaining-seconds)
              :style (m/TextStyle
                      .fontSize (tokens/font-size :xxl)
                      .fontWeight m/FontWeight.bold)})
            (m/SizedBox .height (tokens/space :l))
            (focus-depth/focus-depth-bar
             {:depth current-focus-depth
              :max-depth 1.0
              :width 250.0
              :height 24.0})
            (m/SizedBox .height (tokens/space :xl))
            (if is-paused
              (m/ElevatedButton .onPressed on-resume .child (m/Text "Resume"))
              (m/OutlinedButton .onPressed on-pause .child (m/Text "Pause")))
            (m/SizedBox .height (tokens/space :m))
            (m/Row
             .mainAxisAlignment m/MainAxisAlignment.center
             .children
             [(m/ElevatedButton .onPressed on-complete .child (m/Text "Complete"))
              (m/SizedBox .width (tokens/space :m))
              (m/OutlinedButton .onPressed on-abort .child (m/Text "Abort"))])])))

(defn forecast-list [{:keys [forecasts task-titles-map on-start]}]
  (if (empty? forecasts)
    (widgets/empty-state "No Forecast for today")
    (m/ListView
     .children
     (mapv (fn [f]
             (let [tid (.-taskId f)
                   title (get task-titles-map tid "Loading...")]
               (m/Card
                .margin (m/EdgeInsets.symmetric :vertical (tokens/space :s) :horizontal (tokens/space :m))
                .child (m/ListTile
                        .title (comp/mongol-text-block {:text title})
                        .trailing (m/ElevatedButton
                                   .onPressed (fn [] (on-start f))
                                   .child (m/Text "Start"))))))
           forecasts))))

(defn focus-screen [_params]
  (let [timer (atom nil)
        dao (app-state/get-dao)
        db-ctx {:db (app-state/get-db) :dao dao}]

    (letfn [(load-today-stats []
              (let [now (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch))
                    today-start-ts (int (/ (- now (* 24 60 60 1000)) 1000))
                    today-end-ts (int (/ now 1000))]
                (-> (timeline-db/query-range dao today-start-ts today-end-ts)
                    (.then (fn [events]
                             (let [focus-events (filter #(re-find #"^focus/" (.-type %)) (or events []))
                                   start-count (count (filter #(= (.-type %) "focus/start") focus-events))
                                   complete-count (count (filter #(= (.-type %) "focus/complete") focus-events))
                                   p-count (count (filter #(= (.-type %) "focus/pause") focus-events))
                                   total-minutes (reduce + 0 (map #(or (.-duration %) 0) (filter #(= (.-type %) "focus/complete") focus-events)))]
                               (ui-state/update-focus-state!
                                assoc :today-stats
                                {:total-minutes total-minutes
                                 :session-count start-count
                                 :completed-count complete-count
                                 :pause-count p-count})))))))

            (load-today-forecasts []
              (ui-state/update-focus-state! assoc :loading true)
              (let [today-yyyymmdd (time/ts->yyyymmdd (int (/ (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch)) 1000)))]
                (-> (forecast-db/watch-by-date dao today-yyyymmdd)
                    (.first)
                    (.then (fn [forecasts]
                             (let [fs (or forecasts [])
                                   tids (distinct (map #(.-taskId %) fs))]
                               (ui-state/update-focus-state! assoc :today-forecasts fs)
                               (-> (dart-core/Future.wait
                                    (mapv (fn [tid]
                                            (-> (task-db/get-by-id dao tid)
                                                (.then (fn [t] (when t (ui-state/update-focus-state! update :task-titles-map assoc tid (.-title t)))))))
                                          tids))
                                   (.then (fn [_] (ui-state/update-focus-state! assoc :loading false))))))))))

            (start-focus [f ctx]
              (let [tid (.-taskId f)
                    sid (str "focus-session-" (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch)))
                    title (get (:task-titles-map @ui-state/focus-state) tid "Unknown task")]
                (-> (dispatch/dispatch-event! db-ctx
                                              {:type :focus/start
                                               :payload {:forecast-id (.-id f) :session-id sid :title (str "Start Focus: " title)}
                                               :source :ui})
                    (.then (fn [_]
                             (ui-state/update-focus-state!
                              merge
                              {:current-forecast f
                               :session-id sid
                               :is-focusing true
                               :is-paused false
                               :start-time (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch))
                               :remaining-seconds pomodoro-duration-seconds
                               :pause-count 0
                               :current-focus-depth 0.0})
                             (reset! timer
                                     (async/Timer.periodic
                                      (dart-core/Duration .seconds 1)
                                      (fn [_]
                                        (let [{:keys [is-focusing is-paused remaining-seconds pause-count]} @ui-state/focus-state]
                                          (when (and is-focusing (not is-paused))
                                            (let [new-rem (dec remaining-seconds)
                                                  depth (focus-depth/calculate-focus-depth (- pomodoro-duration-seconds new-rem) pomodoro-duration-seconds pause-count false)]
                                              (ui-state/update-focus-state!
                                               merge
                                               {:current-focus-depth depth
                                                :remaining-seconds (if (<= new-rem 0) 0 new-rem)})))))))
                             (toast/show-success-toast ctx "Focus started"))))))

            (complete-focus [ctx]
              (let [{:keys [current-forecast session-id start-time task-titles-map current-focus-depth]} @ui-state/focus-state
                    dur (int (/ (- (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch)) start-time) 60000))
                    title (get task-titles-map (.-taskId current-forecast) "Unknown task")]
                (when (and current-forecast session-id)
                  (-> (dispatch/dispatch-event! db-ctx
                                                {:type :focus/complete
                                                 :payload {:session-id session-id :duration-minutes dur :title (str "Complete Focus: " title " (" dur " minutes)")}
                                                 :source :ui})
                      (.then (fn [_]
                               (-> (forecast-db/mark-done! dao (.-id current-forecast))
                                   (.then (fn [_]
                                            (toast/show-success-toast ctx "Focus completed")
                                            (ui-state/update-focus-state!
                                             (fn [s]
                                               (-> s
                                                   (assoc :is-focusing false)
                                                   (update :focus-depth-history conj current-focus-depth))))
                                            (when @timer (.cancel @timer) (reset! timer nil))
                                            (load-today-forecasts)
                                            (load-today-stats))))))))))]

      (load-today-forecasts)
      (load-today-stats)

      (f/widget
       :context ctx
       :watch [{:keys [is-focusing is-paused remaining-seconds today-forecasts task-titles-map
                       loading today-stats focus-depth-history current-focus-depth current-forecast]} ui-state/focus-state]
       (m/Scaffold
        .appBar (m/AppBar
                 .title (comp/mongol-text-block {:text "Focus"})
                 .actions [(m/IconButton .icon (m/Icon m/Icons.history) .onPressed (fn [] (toast/show-info-toast ctx "History in development")))]
                 .backgroundColor (tokens/color :bg-focus))
        .backgroundColor (tokens/color :bg-focus)
        .body (m/Column
               .children
               [(today-stats-card {:stats today-stats :depth-history focus-depth-history})
                (m/Expanded
                 .child (if is-focusing
                          (focus-active-session
                           {:task-title (get task-titles-map (.-taskId current-forecast) "Unknown task")
                            :remaining-seconds remaining-seconds
                            :current-focus-depth current-focus-depth
                            :is-paused is-paused
                            :on-resume (fn [] (ui-state/update-focus-state! assoc :is-paused false))
                            :on-pause (fn []
                                        (ui-state/update-focus-state!
                                         (fn [s] (-> s (assoc :is-paused true) (update :pause-count inc)))))
                            :on-complete (fn [] (complete-focus ctx))
                            :on-abort (fn []
                                        (ui-state/update-focus-state! assoc :is-focusing false)
                                        (when @timer (.cancel @timer) (reset! timer nil)))})
                          (if loading
                            (widgets/loading-indicator)
                            (forecast-list {:forecasts today-forecasts :task-titles-map task-titles-map :on-start (fn [f] (start-focus f ctx))}))))]))))))
