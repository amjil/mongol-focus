(ns minii-focus.screens.focus
  "Focus page - display current Focus task"
  (:require
   ["package:flutter/material.dart" :as m]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.components.core :as comp]
   [minii-focus.services.task :as task]
   [minii-focus.states.app-state :as state]))

(defn focus-screen []
  (f/widget
   :watch [{loading :focus-loading
            tasks :focus-tasks
            depth :focus-depth
            session-id :focus-session-id} state/app-state]
   (let [main-view (cond
                     loading
                     (m/Center
                      .child (m/CircularProgressIndicator))

                     (empty? tasks)
                     (m/Center
                      .child (comp/mongol-text-block
                              {:text "No Focus task currently"
                               :style (m/TextStyle
                                       .color (comp/color :text-weak)
                                       .fontSize (comp/font-size :m))}))

                     :else
                     (let [focused-task (first tasks)]
                       (comp/focus-container
                        {:is-focused true
                         :child (m/Column
                                 .mainAxisAlignment m/MainAxisAlignment.center
                                 .crossAxisAlignment m/CrossAxisAlignment.center
                                 .children
                                 [(comp/mongol-text-block
                                   {:text (.-content focused-task)
                                    :style (m/TextStyle
                                            .fontSize (comp/font-size :xl)
                                            .height (comp/line-height (comp/font-size :xl)))})
                                  (m/SizedBox .height (comp/space :l))
                                  (comp/focus-depth-bar
                                   {:depth-score depth
                                    :max-score 100})])})))]
     (m/Scaffold
      .appBar (m/AppBar
               .actions [(m/IconButton
                          .tooltip "Refresh"
                          .icon (m/Icon m/Icons.refresh)
                          .onPressed (fn []
                                       (task/refresh-focus-tasks!)))])
      .body (comp/mongol-row-layout
             {:main-row (comp/vertical-reveal-motion
                         {:child main-view})
              :side-row (m/Container
                         .padding (m/EdgeInsets.all (comp/space :m))
                         .child (m/Column
                                 .mainAxisAlignment m/MainAxisAlignment.center
                                 .crossAxisAlignment m/CrossAxisAlignment.center
                                 .children
                                 [(m/IconButton
                                   .tooltip "Exit Focus"
                                   .icon (m/Icon m/Icons.close)
                                   .onPressed (fn []
                                                (when (and (seq tasks) session-id)
                                                  (let [focused-task (first tasks)]
                                                    (-> (task/exit-focus (.-id focused-task) session-id depth)
                                                        (.then (fn []
                                                                 (state/set-focused-task! nil)
                                                                 (state/set-focus-session-id! nil)
                                                                 (state/update-focus-depth! 0)
                                                                 (task/refresh-focus-tasks!)
                                                                 (task/refresh-today-tasks!)))
                                                        (.catchError (fn [error]
                                                                       (dart-core/print (str "Error exiting focus: " error)))))))))]))})))))

;; NOTE: Loading/refresh is handled by services.task; pages only subscribe to state and trigger refresh via buttons.

