(ns minii-focus.screens.project-list
  "Project list page - display projects and subtask hierarchy"
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["package:cljd_mongol_focus/mongol_expansion_tile.dart" :as expansion]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.components.core :as comp]
   [minii-focus.screens.task-detail :as task-detail]
   [minii-focus.services.task :as task-svc]
   [minii-focus.services.item-tree :as tree-svc]
   [minii-focus.states.app-state :as state]
   [minii-focus.utils.toast :as toast]))

(defn- long-press-fn [ctx pid controller adding-to-project refresh-projects-with-blocked!]
  (m/showModalBottomSheet
   .context ctx
   .builder
   (fn [_]
     (m/Container
      .padding (m/EdgeInsets.only .top (comp/space :s))
      .child (m/Row
              .mainAxisSize m/MainAxisSize.min
              .children
              [(mgl/MongolListTile
                .leading (m/Icon m/Icons.edit)
                .title (mgl/MongolText "Edit project")
                .onTap
                (fn []
                  (task-svc/refresh-task-detail! pid)
                  (m/Navigator.pop ctx)
                  (m/Navigator.push
                   ctx
                   (m/MaterialPageRoute
                    .builder (fn [_]
                               (task-detail/task-detail-screen pid))))
                  nil))
               (mgl/MongolListTile
                .leading (m/Icon m/Icons.add)
                .title (mgl/MongolText "Add subtask")
                .onTap
                (fn []
                  (reset! adding-to-project pid)
                  (set! (.-text controller) "")
                  (m/Navigator.pop ctx)
                  (comp/show-project-input-dialog
                   ctx
                   {:controller controller
                    :title "Add subtask"
                    :on-submit (fn [subtask-content]
                                 (when (and (not (empty? subtask-content)) pid)
                                   (-> (task-svc/create-task subtask-content "task" "active" nil nil)
                                       (.then (fn [task-id]
                                                (when task-id
                                                  (-> (tree-svc/set-parent-task task-id pid)
                                                      (.then (fn [_]
                                                               (dart-core/print (str "Added subtask " task-id " to project " pid))
                                                               (reset! adding-to-project nil)
                                                               (refresh-projects-with-blocked!)
                                                               (task-svc/refresh-projects!)))
                                                      (.catchError (fn [error]
                                                                     (toast/handle-error-with-toast ctx error "Дэд ажлыг нэмэхэд алдаа гарлаа")))))))
                                       (.catchError (fn [error]
                                                      (toast/handle-error-with-toast ctx error "Дэд ажлыг үүсгэхэд алдаа гарлаа"))))))})
                  nil))
               (mgl/MongolListTile
                .leading (m/Icon m/Icons.delete)
                .title (mgl/MongolText "Delete project")
                .onTap
                (fn []
                  (m/Navigator.pop ctx)
                  (m/showDialog
                   .context ctx
                   .builder
                   (fn [_]
                     (m/AlertDialog
                      .content
                      (m/Row
                       .children
                       [(mgl/MongolText "Delete project?")
                        (mgl/MongolText "This will delete the project and all its subtasks.")])
                      .actions
                      [(mgl/MongolTextButton
                        .child (mgl/MongolText "Cancel")
                        .onPressed (fn [] (m/Navigator.pop ctx)))
                       (mgl/MongolTextButton
                        .child (mgl/MongolText "Delete")
                        .onPressed
                        (fn []
                          (-> (task-svc/delete-task pid)
                              (.then (fn [_]
                                       (refresh-projects-with-blocked!)
                                       (task-svc/refresh-projects!)
                                       (m/Navigator.pop ctx)))
                              (.catchError (fn [error]
                                             (toast/handle-error-with-toast ctx error "Төслийг устгахад алдаа гарлаа"))))))])))
                  nil))])))))

(defn- create-subtask-fn [ctx pid adding-to-project refresh-projects-with-blocked! subtask-content]
  (when (and (not (empty? subtask-content)) pid)
    (-> (task-svc/create-task subtask-content "task" "active" nil nil)
        (.then (fn [task-id]
                 (when task-id
                   (-> (tree-svc/set-parent-task task-id pid)
                       (.then (fn [_]
                                (dart-core/print (str "Added subtask " task-id " to project " pid))
                                (reset! adding-to-project nil)
                                (refresh-projects-with-blocked!)
                                (task-svc/refresh-projects!)))
                       (.catchError (fn [error]
                                      (toast/handle-error-with-toast ctx error "Дэд ажлыг нэмэхэд алдаа гарлаа")))))))
        (.catchError (fn [error]
                       (toast/handle-error-with-toast ctx error "Дэд ажлыг үүсгэхэд алдаа гарлаа"))))))

(defn- expansion-tile [ctx pid tasks-with-info
                       refresh-projects-with-blocked!
                       controller adding-to-project proj
                       selection-mode selected-task-ids]
  (let [is-open (atom false)
        child-items (atom [])]
    (f/widget
     :watch [_is-open is-open
             _child-items child-items
             _selection-mode selection-mode
             _selected-ids selected-task-ids]
     (expansion/MongolExpansionTile
      .key (m/ValueKey pid)
      .title (m/GestureDetector
              .onLongPress (fn []
                             ;; Show project menu (edit, add subtask, delete)
                             (long-press-fn ctx pid controller adding-to-project refresh-projects-with-blocked!)
                             nil)
              .child (comp/mongol-text-block
                      {:text (.-content proj)
                       :style (m/TextStyle .fontSize (comp/font-size :l))}))
      .direction expansion/ExpandDirection.right
      .initiallyExpanded _is-open
      .onChanged (fn [v]
                   (-> (tree-svc/get-children-with-items pid)
                       (.then (fn [x]
                                (dart-core/print (str "Child items: " x))
                                (reset! child-items x))))
                   (reset! is-open v))
      .useListView true
      .scrollDirection m/Axis.horizontal
      .childrenPadding (m/EdgeInsets.only .left (comp/space :m))
      .children
      (if (and _child-items (seq _child-items))
        (vec
         (map
          (fn [row]
            (let [item (:item row)
                  item-id (.-id item)
                  task-info (get @tasks-with-info item-id)
                  blocked? (:blocked task-info false)
                  task-status (keyword (.-status item))
                  is-selected (contains? @selected-task-ids item-id)
                  handlers (comp/make-task-handlers
                           ctx item-id item task-status
                           selected-task-ids selection-mode
                           {:on-navigate-to-task (fn [tid]
                                                  (task-svc/refresh-task-detail! tid)
                                                  (m/Navigator.push
                                                   ctx
                                                   (m/MaterialPageRoute
                                                    .builder (fn [_]
                                                               (task-detail/task-detail-screen tid)))))
                            :on-refresh (fn []
                                         (refresh-projects-with-blocked!)
                                         (task-svc/refresh-projects!))
                            :on-long-press-menu (fn [ctx tid _blocked? _parent-id on-refresh]
                                                 (comp/show-item-menu
                                                  ctx tid on-refresh
                                                  {:show-structure? true
                                                   :show-basic? true}))
                            :blocked? blocked?})]
              (comp/mongol-task-item
               (merge
                {:key item-id
                 :content (.-content item)
                 :status task-status
                 :blocked? blocked?
                 :selection-mode _selection-mode
                 :selected is-selected}
                handlers))))
          _child-items))
        [(mgl/MongolListTile
          .title (comp/mongol-text-block
                  {:text "No subtasks"
                   :style (m/TextStyle .color (comp/color :text-weak))})
          .trailing (m/IconButton
                     .icon (m/Icon m/Icons.add)
                     .tooltip "Add subtask"
                     .onPressed (fn []
                                  (reset! adding-to-project pid)
                                  (set! (.-text controller) "")
                                  (comp/show-project-input-dialog
                                   ctx
                                   {:controller controller
                                    :title "Add subtask"
                                    :on-submit (fn [subtask-content]
                                                 (create-subtask-fn ctx pid adding-to-project refresh-projects-with-blocked! subtask-content))})
                                  nil)))])))))

(defn- create-project-fn [ctx refresh-projects-with-blocked! project-content]
  (when (not (empty? project-content))
    (-> (task-svc/create-task project-content "project" "active" nil nil)
        (.then (fn [project-id]
                 (when project-id
                   (dart-core/print (str "Created project: " project-id))
                   (refresh-projects-with-blocked!)
                   (task-svc/refresh-projects!))))
        (.catchError (fn [error]
                       (toast/handle-error-with-toast ctx error "Төслийг үүсгэхэд алдаа гарлаа"))))))


(defn project-list-screen []
  (let [expanded (atom #{})
        children-map (atom {})
        tasks-with-info (atom {})
        controller (m/TextEditingController.)
        adding-to-project (atom nil)
        selection-mode (atom false)
        selected-task-ids (atom #{})]
    (f/widget
     :context ctx
     :watch [{projects :projects loading :projects-loading} state/app-state
             _exp expanded
             _cm children-map
             _twi tasks-with-info
             _selection-mode selection-mode
             _selected-ids selected-task-ids]
     :let [refresh-projects-with-blocked!
           (fn []
             (reset! tasks-with-info {})
             (task-svc/refresh-projects!)
             (-> (task-svc/get-projects)
                 (.then (fn [refreshed-projects]
                          (when (seq refreshed-projects)
                            ;; Load blocked info for all child tasks
                            (let [all-child-tasks (->> @children-map
                                                        vals
                                                        (mapcat (fn [rows] (map :item rows)))
                                                        (vec))]
                              (when (seq all-child-tasks)
                                (-> (task-svc/get-tasks-with-blocked-info all-child-tasks)
                                    (.then (fn [info-list]
                                             (reset! tasks-with-info
                                                     (reduce (fn [acc info]
                                                               (assoc acc (.-id (:task info)) info))
                                                             {} info-list))))
                                    (.catchError (fn [error]
                                                   (toast/handle-error-with-toast ctx error "Хориглосон мэдээллийг ачаалахад алдаа гарлаа")))))))))
                 (.catchError (fn [error]
                                (toast/handle-error-with-toast ctx error "Төслүүдийг шинэчлэхэд алдаа гарлаа")))))
           main-view (cond
                      loading (m/Center .child (m/CircularProgressIndicator))
                      (empty? projects) (m/Center
                                         .child (comp/mongol-text-block
                                                 {:text "No projects"
                                                  :style (m/TextStyle .color (comp/color :text-weak))}))
                      :else
                      (m/ListView
                       .scrollDirection m/Axis.horizontal
                       .padding (m/EdgeInsets.all (comp/space :m))
                       .children
                       (map (fn [proj]
                              (let [pid (.-id proj)]
                                (expansion-tile
                                 ctx pid
                                 tasks-with-info
                                 refresh-projects-with-blocked! controller
                                 adding-to-project proj
                                 selection-mode selected-task-ids)))
                            projects)))]
     (do
       ;; Load blocked info when children are loaded
       (when (and (not loading) (seq projects))
         (let [all-child-tasks (->> @children-map
                                     vals
                                     (mapcat (fn [rows] (map :item rows)))
                                     (vec))]
           (when (and (seq all-child-tasks) (empty? @tasks-with-info))
             (-> (task-svc/get-tasks-with-blocked-info all-child-tasks)
                 (.then (fn [info-list]
                          (reset! tasks-with-info
                                  (reduce (fn [acc info]
                                            (assoc acc (.-id (:task info)) info))
                                          {} info-list))))
                 (.catchError (fn [error]
                                (dart-core/print (str "Error loading blocked info: " error))))))))
       (m/Scaffold
        .appBar (m/AppBar
                 .actions [(if _selection-mode
                             (m/IconButton
                              .tooltip "Cancel selection"
                              .icon (m/Icon m/Icons.close)
                              .onPressed (fn []
                                           (reset! selected-task-ids #{})
                                           (reset! selection-mode false)))
                             (m/IconButton
                              .tooltip "Select tasks"
                              .icon (m/Icon m/Icons.check_box)
                              .onPressed (fn []
                                           (reset! selection-mode true))))
                           (if (not _selection-mode)
                             (m/IconButton
                              .tooltip "Add project"
                              .icon (m/Icon m/Icons.add)
                              .onPressed (fn []
                                           (set! (.-text controller) "")
                                           (comp/show-project-input-dialog
                                            ctx
                                            {:controller controller
                                             :title "New project"
                                             :on-submit (fn [project-content]
                                                          (create-project-fn ctx refresh-projects-with-blocked! project-content))})))
                             (m/SizedBox))
                           (if (not _selection-mode)
                             (m/IconButton
                              .tooltip "Refresh"
                              .icon (m/Icon m/Icons.refresh)
                              .onPressed (fn []
                                           (refresh-projects-with-blocked!)))
                             (m/SizedBox))])
        .body (m/Column
               .children
               [(comp/batch-operations-toolbar ctx selected-task-ids selection-mode refresh-projects-with-blocked!)
                (m/Expanded
                 .child (comp/mongol-row-layout
                        {:main-row (comp/vertical-reveal-motion
                                    {:child main-view})
                         :side-row (comp/projects-overview-panel projects (vals @tasks-with-info))}))]))))))
