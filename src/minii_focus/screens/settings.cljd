(ns minii-focus.screens.settings
  "Settings page"
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.core.components.core :as comp]
   [minii-focus.core.components.tokens :as tokens]
   [minii-focus.core.state.app-state :as app-state]
   [minii-focus.core.state.ui-state :as ui-state]
   [minii-focus.core.db.settings :as settings-db]
   [minii-focus.core.utils.toast :as toast]
   [minii-focus.widgets.common :as widgets]))

(defn forecast-settings-section [{:keys [dao on-updated]}]
  (f/widget
   :watch [daily-capacity-val (future (settings-db/get-value dao "daily_capacity"))
           {:keys [daily-capacity]} ui-state/settings-state]
   :let [daily-capacity (or (some-> daily-capacity-val dart-core/double.tryParse) daily-capacity)]
   (m/Card
    .margin (m/EdgeInsets.all (tokens/space :m))
    .child (m/Padding
            .padding (m/EdgeInsets.all (tokens/space :m))
            .child (m/Column
                    .crossAxisAlignment m/CrossAxisAlignment.start
                    .children
                    [(comp/mongol-text-block {:text "Forecast Settings" :style (m/TextStyle .fontSize (tokens/font-size :l) .fontWeight m/FontWeight.bold)})
                     (m/SizedBox .width (tokens/space :m))
                     (m/Column
                      .crossAxisAlignment m/CrossAxisAlignment.start
                      .children
                      [(comp/mongol-text-block {:text (str "Daily Capacity: " (int daily-capacity))})
                       (m/SizedBox .width (tokens/space :m))
                       ;; Slider in vertical column needs a container with height
                       (m/SizedBox .height 200.0 .child (m/RotatedBox .quarterTurns 1 .child (m/Slider .value daily-capacity .min 1.0 .max 10.0 .divisions 9 .onChanged (fn [v] (ui-state/update-settings-state! assoc :daily-capacity v)))))
                       (m/SizedBox .width (tokens/space :m))
                       (m/ElevatedButton .onPressed (fn [] (-> (settings-db/set-value! dao "daily_capacity" (str daily-capacity)) (.then (fn [_] (on-updated))))) .child (comp/mongol-text-block {:text "Save"}))])])))))

(defn app-info-section []
  (m/Card
   .margin (m/EdgeInsets.all (tokens/space :m))
   .child (m/Padding
           .padding (m/EdgeInsets.all (tokens/space :m))
           .child (m/Row
                   .crossAxisAlignment m/CrossAxisAlignment.start
                   .children
                   [(comp/mongol-text-block {:text "App Information" :style (m/TextStyle .fontSize (tokens/font-size :l) .fontWeight m/FontWeight.bold)})
                    (m/SizedBox .width (tokens/space :s))
                    (comp/mongol-text-block {:text "Mongol Focus v1.0.0"})]))))

(defn settings-screen [_params]
  (let [dao (app-state/get-dao)]
    (f/widget
     (m/Scaffold)
     .body
     (m/SafeArea)
     (m/ListView
      .scrollDirection m/Axis.horizontal
      .children
      [(if dao
         (forecast-settings-section {:dao dao :on-updated (fn [] (toast/show-success-toast nil "Updated"))})
         (widgets/loading-indicator))
       (app-info-section)
       (m/Card
        .margin (m/EdgeInsets.all (tokens/space :m))
        .child (mgl/MongolListTile
                .leading (m/Icon m/Icons.download)
                .title (comp/mongol-text-block {:text "Export Data"})
                .onTap (fn [] (toast/show-info-toast nil "Exporting..."))))]))))
