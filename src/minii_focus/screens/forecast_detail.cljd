(ns minii-focus.screens.forecast-detail
  "Forecast Detail page"
  (:require
   ["package:flutter/material.dart" :as m]
   ["dart:core" :as dart-core]
   [cljd.flutter :as f]
   [minii-focus.core.components.core :as comp]
   [minii-focus.core.components.tokens :as tokens]
   [minii-focus.core.state.app-state :as app-state]
   [minii-focus.core.state.ui-state :as ui-state]
   [minii-focus.core.db.forecast :as forecast-db]
   [minii-focus.core.db.task :as task-db]
   [minii-focus.core.utils.time :as time]
   [minii-focus.widgets.common :as widgets]))

(defn stat-card [{:keys [title value subtitle color]}]
  (m/Card
   .margin (m/EdgeInsets.symmetric :vertical (tokens/space :xs) :horizontal (tokens/space :m))
   .child (m/Padding
           .padding (m/EdgeInsets.all (tokens/space :m))
           .child (m/Column
                   .crossAxisAlignment m/CrossAxisAlignment.start
                   .children
                   (filterv some?
                    [(comp/mongol-text-block {:text title :style (m/TextStyle .fontSize (tokens/font-size :s) .color (tokens/color :text-weak))})
                     (m/SizedBox .height (tokens/space :xs))
                     (comp/mongol-text-block {:text (str value) :style (m/TextStyle .fontSize (tokens/font-size :xl) .fontWeight m/FontWeight.bold .color (or color (tokens/color :text-main)))})
                     (when subtitle (comp/mongol-text-block {:text subtitle :style (m/TextStyle .fontSize (tokens/font-size :s) .color (tokens/color :text-weak))}))])))))

(defn forecast-detail-item [{:keys [forecast task-title]}]
  (m/Card
   .margin (m/EdgeInsets.symmetric :vertical (tokens/space :xs) :horizontal (tokens/space :m))
   .child (m/Padding
           .padding (m/EdgeInsets.all (tokens/space :m))
           .child (m/Row
                   .children
                   (filterv some?
                    [(m/Expanded .child (comp/mongol-text-block {:text task-title :style (m/TextStyle .fontSize (tokens/font-size :m) .decoration (when (.-done forecast) m/TextDecoration.lineThrough))}))
                     (when (.-done forecast) (m/Icon m/Icons.check_circle .color m/Colors.green))])))))

(defn forecast-detail-screen [params]
  (let [date-yyyymmdd (if-let [id (:id params)] (int id) (time/ts->yyyymmdd (int (/ (-> (dart-core/DateTime.now) (.-millisecondsSinceEpoch)) 1000))))
        dao (app-state/get-dao)]
    (f/widget
     :watch [forecasts (stream
                        (map (fn [fs]
                               (let [tids (distinct (map #(.-taskId %) (or fs [])))]
                                 (doseq [tid tids]
                                   (when-not (get (:titles @ui-state/forecast-detail-state) tid)
                                     (-> (task-db/get-by-id dao tid)
                                         (.then (fn [t] (when t (ui-state/update-forecast-detail-state! update :titles assoc tid (.-title t)))))))))
                               fs))
                        :as-values (forecast-db/watch-by-date dao date-yyyymmdd))
             {:keys [titles]} ui-state/forecast-detail-state]
     :let [loading (nil? forecasts)
           forecasts (or forecasts [])]
     (m/Scaffold
      .appBar (m/AppBar)
      .body (if loading (widgets/loading-indicator)
                (m/ListView
                 .children
                 [(m/Container .padding (m/EdgeInsets.all (tokens/space :l)) .child (comp/mongol-text-block {:text (widgets/format-yyyymmdd date-yyyymmdd) :style (m/TextStyle .fontSize (tokens/font-size :xl) .fontWeight m/FontWeight.bold)}))
                  (stat-card {:title "Total" :value (count forecasts)})
                  (stat-card {:title "Done" :value (count (filter #(.-done %) forecasts)) :color m/Colors.green})
                  (m/Column .children (mapv (fn [f] (forecast-detail-item {:forecast f :task-title (get titles (.-taskId f) "Loading...")})) forecasts))]))))))
