(ns minii-focus.widgets.search-button
  (:require [cljd.flutter :as f]
            [clojure.string :as str]
            ["package:flutter/material.dart" :as m]
            [minii-focus.state.store :as store]
            [minii-focus.logic.search-actions :as search-actions]))

(defn filter-aware-search-button []
  (f/widget
   :context ctx
   :watch [{:keys [search-term selected-category-id selected-due-date]} store/app-ui-state]
   :get {{{primary .-primary on-surface .-onSurface surface .-surface} .-colorScheme} m/Theme}
   :let [is-filtered (or (not (str/blank? search-term))
                         (some? selected-category-id)
                         (some? selected-due-date))]
   (m/Stack
    .alignment m/Alignment.center
    .children
    [(m/IconButton
      .icon (m/Icon (if is-filtered m/Icons.search_rounded m/Icons.search_outlined)
                    .color (if is-filtered primary on-surface))
      .tooltip (cond 
                 (and (not (str/blank? search-term)) selected-category-id selected-due-date) "Хайлт, ангилал, огноо шүүлтүүр идэвхтэй"
                 (and (not (str/blank? search-term)) selected-category-id) "Хайлт ба ангилал шүүлтүүр идэвхтэй"
                 (and (not (str/blank? search-term)) selected-due-date) "Хайлт ба огноо шүүлтүүр идэвхтэй"
                 (and selected-category-id selected-due-date) "Ангилал ба огноо шүүлтүүр идэвхтэй"
                 (not (str/blank? search-term)) "Хайлт идэвхтэй"
                 selected-category-id "Ангилал шүүлтүүр идэвхтэй"
                 selected-due-date "Огноо шүүлтүүр идэвхтэй"
                 :else "Хайх")
      .onPressed (fn [] (search-actions/open-search-dialog ctx) nil))
     
     ;; When filter is active, show an indicator dot
     (if is-filtered
       (m/Positioned
        .top 12.0
        .right 12.0
        .child (m/Container
                .width 10.0
                .height 10.0
                .decoration (m/BoxDecoration
                             .color primary
                             .shape m/BoxShape.circle
                             .border (m/Border.all .color surface .width 2.0))))
       (m/SizedBox.shrink))])))