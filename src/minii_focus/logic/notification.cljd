(ns minii-focus.logic.notification
  (:require ["package:flutter_local_notifications/flutter_local_notifications.dart" :as n]
            ["package:timezone/timezone.dart" :as tz]
            ["package:timezone/data/latest.dart" :as tz_data]
            ["package:flutter_timezone/flutter_timezone.dart" :as fntz]
            ["dart:io" :as io]
            ["dart:core" :as dc]
            ["package:cljd_mongol_focus/notification_helper.dart" :as helper]))

(defonce ^:private plugin (n/FlutterLocalNotificationsPlugin.))

(defn init!
  "Initialize notification service, called when main function starts"
  []
  ;; 1. Initialize timezone (required for scheduled notifications)
  (tz_data/initializeTimeZones)
  ;; 1.1 Set device local timezone (default is UTC, would cause wrong notification time!)
  (try
    (let [tz-info (await (.getLocalTimezone fntz/FlutterTimezone))
          tz-name (.-identifier tz-info)
          location (tz/getLocation tz-name)]
      (tz/setLocalLocation location))
    (catch Exception _ nil))
  (let [;; 2. Android initialization settings
        ;; @mipmap/ic_launcher is the app icon, make sure your Android project has this icon
        android-settings (n/AndroidInitializationSettings. "@mipmap/ic_launcher")

        ;; 3. iOS / Darwin initialization settings
        ios-settings (n/DarwinInitializationSettings.
                      .requestAlertPermission true
                      .requestBadgePermission true
                      .requestSoundPermission true)

        init-settings (n/InitializationSettings.
                       .android android-settings
                       .iOS ios-settings)]

    ;; 4. Execute plugin initialization
    (await (.initialize plugin init-settings
                        .onDidReceiveNotificationResponse
                        (fn [response]
                          ;; Handle logic after user clicks notification here
                          (dart:core/print (str "Notification clicked: " (.-payload response))))))))

(defn request-permissions!
  "Request permissions on Android 13+ and iOS"
  []
  (if (io/Platform.isAndroid)
    ;; Use helper function to call generic method with type parameter
    (let [android-plugin (helper/resolveAndroidImpl plugin)]
      (when android-plugin
        (await (.requestNotificationsPermission android-plugin))))
    (when (io/Platform.isIOS)
      ;; Use helper function to call generic method with type parameter
      (let [ios-plugin (helper/resolveIOSImpl plugin)]
        (when ios-plugin
          (await (.requestPermissions ios-plugin .alert true .badge true .sound true)))))))

(defn schedule-task-reminder!
  "Schedule a reminder for a specific task"
  [id title ^dc/DateTime scheduled-date]
  (let [android-details (n/AndroidNotificationDetails. 
                         "task_reminders" "Task Reminders" 
                         .channelDescription "For sending scheduled reminders for tasks"
                         .importance n/Importance.max 
                         .priority n/Priority.high)
        ;; iOS needs DarwinNotificationDetails, otherwise notification won't show
        ios-details (n/DarwinNotificationDetails.)
        notification-details (n/NotificationDetails. 
                              .android android-details 
                              .iOS ios-details)
        ;; Convert Dart's DateTime to TZDateTime
        tz-date (tz/TZDateTime.from scheduled-date tz/local)]
    
    (await 
     (.zonedSchedule
      plugin
      id       ;; Use task ID as notification ID for easy cancellation later
      title    ;; Notification title
      "ᠲᠠᠨ ᠎ᠤ ᠳᠠᠭᠠᠯᠭᠠᠪᠤᠷᠢ᠎ᠶᠢᠨ ᠴᠠᠭ ᠪᠣᠯᠤᠯ᠎ᠠ !" ;; Notification content (Mongolian: Your task time has arrived)
      tz-date
      notification-details
      .androidScheduleMode n/AndroidScheduleMode.exactAllowWhileIdle))))

(defn cancel-task-reminder!
  "Cancel a task reminder"
  [id]
  (await (.cancel plugin id)))