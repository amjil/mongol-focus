(ns minii-focus.pages.settings
  (:require
   ["dart:core" :as dc]
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [minii-focus.state.store :as store] ;; Import your store
   [minii-focus.logic.update :as update]
   [minii-focus.logic.feedback :as feedback]
   [components.bottom-sheet :as bs]
   [components.setting :as sett]))

(defn- settings-header [color-scheme]
  [(m/Container
    .width 110
    .height 110
    .decoration (m/BoxDecoration
                 .color (.-primary color-scheme)
                 .shape m.BoxShape/circle)
    .child (m/Icon
            m.Icons/settings
            .size 56
            .color (.-onPrimary color-scheme)))

   (m/Container .width 12)

   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children [(mgl/MongolText "ᠲᠣᠬᠢᠷᠠᠭ᠎ᠠ"
                               .style (m/TextStyle
                                       .fontSize 26
                                       .fontWeight m.FontWeight/bold
                                       .color (.-onSurface color-scheme)))
               (m/Container .width 8)])

   (m/Container .width 24)])

(defn- open-font-size-dialog [ctx]
  (bs/bottom-sheet
   ctx
   [{:type :header :title "ᠹᠤᠨᠲ᠎ᠤᠨ ᠬᠡᠮᠵᠢᠶ᠎ᠡ "}
    {:title "ᠵᠢᠵᠢᠭ"
     :on-tap #(store/update-setting! :font-size "ᠵᠢᠵᠢᠭ")}
    {:title "ᠳᠤᠮᠳᠠ"
     :on-tap #(store/update-setting! :font-size "ᠳᠤᠮᠳᠠ")}
    {:title "ᠲᠣᠮᠣ"
     :on-tap #(store/update-setting! :font-size "ᠲᠣᠮᠣ")}]
   :height-factor 0.35))

(defn- log-action [label]
  (dc/print (str "Settings action: " label)))

(defn- open-font-family-dialog [ctx]
  (bs/bottom-sheet
   ctx
   [{:type :header :title "ᠹᠣᠨᠲ᠎ᠤᠨ ᠲᠥᠷᠥᠯ"}
    {:title "OyunQaganTig"
     :on-tap #(store/update-setting! :font-family "OyunQaganTig")}
    {:title "OyunAgulaTig"
     :on-tap #(store/update-setting! :font-family "OyunAgulaTig")}
    {:title "OyunGarbiqimelTig"
     :on-tap #(store/update-setting! :font-family "OyunGarbiqimelTig")}
    {:title "OyunGarqagTig"
     :on-tap #(store/update-setting! :font-family "OyunGarqagTig")}
    {:title "OyunGurbanUlusTig"
     :on-tap #(store/update-setting! :font-family "OyunGurbanUlusTig")}
    {:title "OyunHaraTig"
     :on-tap #(store/update-setting! :font-family "OyunHaraTig")}
    {:title "OyunHawangTig"
     :on-tap #(store/update-setting! :font-family "OyunHawangTig")}
    {:title "OyunSoninTig"
     :on-tap #(store/update-setting! :font-family "OyunSoninTig")}]
   :height-factor 0.5))

(defn- open-app-info-sheet [ctx]
  (let [version (await (update/get-current-version!))]
    (bs/bottom-sheet
     ctx
     [{:type :header :title ""}
      {:title (str "Version: " (or version "unknown"))
       :on-tap #(log-action "version")}
      {:title "ᠲᠤᠰᠠᠯᠠᠮᠵᠢ ᠪᠠ ᠰᠠᠨᠠᠯ"
       :on-tap #(do (m/Navigator.pop ctx) (feedback/open-feedback-dialog! ctx))}
      {:title "ᠦᠨᠡᠯᠡᠭᠡ ᠥᠭᠬᠦ"
       :on-tap #(log-action "rate-app")}]
     :height-factor 0.35)))

(defn settings-page []
  (f/widget
   :context ctx
   ;; Watch global state, state variable will contain the current value of atom
   :watch [state store/app-ui-state]
   :get {{{:flds [surface] :as color-scheme} .-colorScheme} m/Theme}
  :let [font-size (get-in state [:settings :font-size])
        font-family (get-in state [:settings :font-family])]
   (m/Scaffold.
    .backgroundColor surface
    .body
    (m/SingleChildScrollView
     .scrollDirection m.Axis/horizontal
     .padding (m.EdgeInsets/all 16)
     .child
     (m/Row
      .crossAxisAlignment m.CrossAxisAlignment/start
      .children
      (concat
       (settings-header color-scheme)
       [
        ;; --- Group 1: Category Management ---
        (sett/settings-section
         "ᠠᠩᠭᠢᠯᠠᠯ"
         [{:title "ᠠᠩᠭᠢᠯᠠᠯ ᠲᠣᠬᠢᠷᠠᠭᠤᠯᠬᠤ"
           :leading m.Icons/folder_special
           :show-arrow? true
           :on-tap (fn [] (m/Navigator.pushNamed ctx "/category-management"))}])

        ;; --- Group 2: Interface Preferences ---
        (sett/settings-section
         "ᠬᠠᠷᠠᠭᠳᠠᠴᠠ" ; Appearance
         [{:title "ᠬᠠᠷᠠᠩᠭᠤᠢ ᠭᠣᠷᠢᠮ"
           :leading m.Icons/dark_mode
           :value (get-in state [:settings :dark-mode?])
           :on-changed #(store/update-setting! :dark-mode? %)}
          {:title "ᠹᠤᠨᠲ᠎ᠤᠨ ᠬᠡᠮᠵᠢᠶ᠎ᠡ "
           :subtitle (str "ᠣᠳᠣ᠎ᠶᠢᠨ: " font-size)
           :leading m.Icons/text_fields
           :show-arrow? true
           :on-tap #(open-font-size-dialog ctx)}

          {:title "ᠹᠣᠨᠲ᠎ᠤᠨ ᠲᠥᠷᠥᠯ"
           :subtitle (str "ᠣᠳᠣ᠎ᠶᠢᠨ: " font-family)
           :leading m.Icons/font_download
           :show-arrow? true
           :on-tap #(open-font-family-dialog ctx)}])

        ;; --- Group 3: App Information ---
        (sett/settings-section
         "ᠠᠫᠫ"
         [{:title "ᠠᠫᠫ᠎ᠤᠨ ᠲᠤᠬᠠᠢ   "
           :subtitle "ᠬᠤᠪᠢᠯᠪᠤᠷᠢ᠂ ᠲᠤᠰᠠᠯᠠᠮᠵᠢ ᠪᠠ ᠰᠠᠨᠠᠯ᠂ ᠦᠨᠡᠯᠡᠭᠡ ᠥᠭᠬᠦ"
           :leading m.Icons/info_outline
           :show-arrow? true
           :on-tap #(open-app-info-sheet ctx)}
          {:title "ᠰᠠᠨᠠᠯ ᠪᠢᠴᠢᠬᠤ"
           :subtitle "ᠰᠠᠨᠠᠯ ᠪᠢᠴᠢᠬᠤ ᠶᠢᠨ ᠳᠡᠯᠭᠡᠷᠡᠩᠭᠦᠢ ᠪᠢᠴᠢᠬᠤ"
           :leading m.Icons/feedback
           :show-arrow? true
           :on-tap #(feedback/open-feedback-dialog! ctx)}
          {:title "ᠰᠢᠨᠡᠴᠢᠯᠡᠯᠲᠡ᠎ᠶᠢ ᠰᠢᠯᠭᠠᠬᠤ"
           :subtitle "ᠰᠢᠨ᠎ᠡ ᠬᠤᠪᠢᠯᠪᠤᠷᠢ᠎ᠶᠢ ᠰᠢᠯᠭᠠᠬᠤ"
           :leading m.Icons/system_update
           :show-arrow? true
           :on-tap #(update/check-for-update! ctx {:manual? true})}])]))))))